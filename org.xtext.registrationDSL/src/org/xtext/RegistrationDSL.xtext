grammar org.xtext.RegistrationDSL with org.eclipse.xtext.common.Terminals

generate registrationDSL "http://www.xtext.org/RegistrationDSL"

Registationsystem: //car rental version
	'system' name =ID '{' declarations += Declaration* '}'
;
Declaration:
	Entity | Workflow | External
;

Entity:
	'entity' name=ID (':' base=[Entity])? '{' fields += Field* '}'
;

Field:
	Attribute | Relation | Require
;

Attribute:
	'attribute' name=ID ':' type=ID
;

Relation:
	'relation' name=ID ':' target=[Entity]
;

Require:
	'require' logic=Logic	
;

External:
	'external' name=ID '(' parameters += ID (',' parameters += ID)* ')'
;

Workflow:
	'workflow' name=ID '{' statements += Statement* '}'
;

Statement:
	Select | Add | Register 
;

Select:
	'select' selectType=[Entity] entityName=ID 
;

Add: 
	'add' selectedEntityName=ID 'to' toEntity=ID '.' toEntityRelation=ID 
;

Register:
	'register' type=[Entity] name=ID
;

Logic returns LogicExp:
	Conjunction ('||' {Or.left=current} right=Conjunction)*
;

Conjunction returns LogicExp:
	PrimLogic ('&&' {And.left=current} right=PrimLogic)*
;

PrimLogic returns LogicExp:
	ExternalCall | Comparison | '[' logic=Logic ']'
;

ExternalCall:
	target=[External] '(' arguments += Exp (',' arguments += Exp)* ')'
;

Comparison:
	left=Exp op=CompareOp right=Exp
;

CompareOp:
	'<' | '>' | '==' | '<=' | '>='
;

Exp returns Expression:
	Factor (('+' {Plus.left=current}| '-' {Minus.left=current}) right=Factor)*
;

Factor returns Expression:
	PrimExp (('*' {Mult.left=current} | '/' {Div.left=current}) right=PrimExp)*
;

PrimExp returns Expression:
	Var | Constant | '(' exp=Exp ')'
;

Var:
	{Variable} name=[Attribute]
;

Constant:
	value=INT
;

